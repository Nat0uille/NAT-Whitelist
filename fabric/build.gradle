plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'java'
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "nat-whitelist" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')

    minecraft "com.mojang:minecraft:1.21.4"
    mappings "net.fabricmc:yarn:1.21.4+build.3:v2"
    modImplementation "net.fabricmc:fabric-loader:0.16.10"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.111.0+1.21.4"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('fabric.mod.json') {
        expand props
    }
}


def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}


jar {
    destinationDirectory = file("$rootDir/jar")
    archiveFileName = "${rootProject.name}-${version}-fabric.jar"
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') && !it.name.contains('fabric-api') && !it.name.contains('fabric-loader') && !it.name.contains('minecraft') }.collect { zipTree(it) }
    }
    from(project(':common').sourceSets.main.output)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
