apply plugin: 'java'

group = 'fr.Nat0uille'

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly("net.md-5:bungeecord-api:1.21-R0.3")
    implementation project(':common')
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'com.h2database:h2:2.1.214'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.4'
    implementation 'net.kyori:adventure-text-minimessage:4.18.0'
    implementation 'net.kyori:adventure-platform-bungeecord:4.3.4'
}


def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    expand props
    filteringCharset 'UTF-8'
    filesMatching('bungee.yml') {
        expand props
    }

    // Copier les ressources du module common
    from('../common/src/main/resources') {
        include 'config.yml'
        include 'languages/**'
    }
}

jar {
    destinationDirectory = file("$rootDir/jar")
    archiveFileName = "${rootProject.name}-${version}-bungee.jar"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}


